-- paths to metamodels for content assist
--@path HAL=/HAL/model/hal.ecore
--@path URN=/seg.jUCMNav/src/seg/jUCMNav/emf/urn.ecore

module Hal2Urn;
create OUT : URN from IN : HAL;

rule hal2urn {
	from 
		h : HAL!HAL
	to 
		u : URN!URNspec (
				
		),
		d: URN!URNdefinition (
			-- set u as parent of d	
			urnspec <- u
		)
	do {
		thisModule.HAL <- h;
			
		
		for (roo in h.rooms) {
			for (dev in roo.devices) {
				d.components <- thisModule.NewComponent(roo, dev);		
			}	
		}
	
	}

}

-- AutomationRule 2 UCMmap
rule AutomationRule2UCMmap {
	from
		ar: HAL!AutomationRule
	to
		um: URN!UCMmap (
			name <- ar.name,
			urndefinition <- thisModule.resolveTemp(ar.eContainer(), 'd')
		),
		sp: URN!StartPoint (
				name <- 'checkRule',
				diagram <- um
			)
}

-- Command 2 Responsibility
rule Command2Responsibility {
	from 
		com: HAL!Command
	to
		res: URN!Responsibility(
				name <- com.name,
				urndefinition <- thisModule.resolveTemp(thisModule.HAL, 'd')
			)
}

-- Components
rule NewComponent(r: HAL!Room, d: HAL!Device) {
	to 
		c: URN!Component(
				name <- r.name + '.' + d.name	
			)
	do {
		c;	
	}
}

rule AtomicRelationalTerm2Stub {
	from
		art: HAL!AtomicRelationalTerm
	to
		st: URN!Stub (
				diagram <- thisModule.resolveTemp(art.eContainer(), 'um')
		)
	do {
		st.name <- art.sensor.name + ' IN ' + art.sensor.eContainer().name + ' ' + art.operator + ' ' + art.comparedAgainstValue;
	}
}





